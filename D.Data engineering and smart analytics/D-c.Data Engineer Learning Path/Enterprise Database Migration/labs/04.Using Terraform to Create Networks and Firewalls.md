
```bash
mkdir terraform-networks
cd terraform-networks

touch provider.tf
touch terraform.tfvars
touch test-server-linux.tf
touch variables.tf
touch vpc-network-public.tf
touch vpc-firewall-rules-public.tf
touch public-test-server-linux.tf
touch random-id-generator.tf
```

```tf
# provider.tf
terraform {
  required_version = ">= 0.12"
}
provider "google" {
  project = var.project_id
  region  = var.gcp_region_1
  zone    = var.gcp_zone_1
}
```

```tf
# variables.tf
# GCP Project ID
variable "project_id" {
  type = string
  description = "GCP Project ID"
}
# Region to use for Subnet 1
variable "gcp_region_1" {
  type = string
  description = "GCP Region"
}
# Zone used for VMs
variable "gcp_zone_1" {
  type = string
  description = "GCP Zone"
}
# Define subnet for public network
variable "subnet_cidr_public" {
  type = string
  description = "Subnet CIDR for Public Network"
}
```

```tf
# terraform.tfvars
# GCP Settings
project_id    = "your-project-id"
gcp_region_1  = "us-central1"
gcp_zone_1    = "us-central1-a"
# GCP Network Variables
subnet_cidr_public  = "10.1.1.0/24"
```

```tf
# vpc-network-public.tf
resource "google_compute_network" "public-vpc" {
  name                    = "public-vpc"
  auto_create_subnetworks = "false"
  routing_mode            = "GLOBAL"
}
resource "google_compute_subnetwork" "public-subnet_1" {
  name          = "public-subnet-1"
  ip_cidr_range = var.subnet_cidr_public
  network       = google_compute_network.public-vpc.name
  region        = var.gcp_region_1
}
```

```bash
cd ~/terraform-networks
terraform init
terraform plan
terraform apply -auto-approve
terraform destroy -auto-approve
```

```tf
# vpc-firewall-rules-public.tf
# allow ssh
resource "google_compute_firewall" "public-allow-ssh" {
  name    = "${google_compute_network.public-vpc.name}-allow-ssh"
  network = google_compute_network.public-vpc.name
  allow {
    protocol = "tcp"
    ports    = ["22"]
  }
  source_ranges = [
    "0.0.0.0/0"
  ]
  target_tags = ["allow-ssh"]
}

# allow rdp
resource "google_compute_firewall" "public-allow-rdp" {
  name    = "${google_compute_network.public-vpc.name}-allow-rdp"
  network = google_compute_network.public-vpc.name
  allow {
    protocol = "tcp"
    ports    = ["3389"]
  }
  source_ranges = [
    "0.0.0.0/0"
  ]
  target_tags = ["allow-rdp"]
}

# allow ping only from everywhere
resource "google_compute_firewall" "public-allow-ping" {
  name    = "${google_compute_network.public-vpc.name}-allow-ping"
  network = google_compute_network.public-vpc.name
  allow {
    protocol = "icmp"
  }
  source_ranges = [
    "0.0.0.0/0"
  ]
}
```

```bash
terraform plan
terraform apply -auto-approve
```

```tf
# random-id-generator.tf
# Terraform plugin for creating random ids
resource "random_id" "instance_id" {
 byte_length = 4
}
```

```tf
# test-server-linux.tf
# Create Test Server in Public VPC
resource "google_compute_instance" "test-server-linux" {
  name         = "public-test-server-linux-${random_id.instance_id.hex}"
  machine_type = "f1-micro"
  zone         = var.gcp_zone_1
  tags         = ["allow-ssh"]
  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-9"
    }
  }
  metadata_startup_script = "sudo apt-get update;"
  network_interface {
    network        = google_compute_network.public-vpc.name
    subnetwork     = google_compute_subnetwork.public-subnet_1.name
    access_config { }
  }
}
output "test-server-linux" {
  value = google_compute_instance.test-server-linux.name
}
output "test-server-linux-external-ip" {
  value = google_compute_instance.test-server-linux.network_interface.0.access_config.0.nat_ip
}
output "test-server-linux-internal-ip" {
  value = google_compute_instance.test-server-linux.network_interface.0.network_ip
}
```